//===========================================================================
//
// Decorations
//
//===========================================================================

ACTOR CRock1 4998
{
	Radius 16
	Height 36
	+SOLID
	States
	{
	Spawn:
		RCK1 A -1
	}
}

ACTOR CTree1 4999
{
	Radius 16
	Height 115
	+SOLID
	States
	{
	Spawn:
		TRE3 A -1
	}
}

//===========================================================================
//
// CodPossessed
//
//===========================================================================
ACTOR CoDPossessed : ZombieMan 5003
{
	DropItem ""
	
	States
	{
	Death:
	XDeath:
		POSS M 5
		POSS N 5 A_XScream
		POSS O 0 A_Fall
		POSS O 5 A_SpawnItem("LostSoul", 1, 16)
		POSS PQRST 5
		POSS U -1
		Stop
	}
}

//===========================================================================
//
// CodSergeant
//
//===========================================================================
ACTOR CoDSergeant : ShotgunGuy 5004
{
	Dropitem ""
	
	States
	{
	Death:
	XDeath:
		SPOS M 5
		SPOS N 5 A_XScream
		SPOS O 0 A_Fall
		SPOS O 5 A_SpawnItem("LostSoul", 1, 16)
		SPOS PQRST 5
		SPOS U -1
		Stop
	}	
}

//===========================================================================
//
// CodChainGuy
//
//===========================================================================
ACTOR CoDChaingunGuy : ChaingunGuy 5005
{
	Dropitem ""
	
	States
	{
	Death:
	XDeath:
		CPOS O 5 
		CPOS P 5 A_XScream
		CPOS Q 0 A_Fall
		CPOS Q 5 A_SpawnItem("LostSoul", 1, 16)
		CPOS RS 5
		CPOS T -1
		Stop
	}	
}

//===========================================================================
//
// CodKnight
// Invisible Hell Knight
//
//===========================================================================
ACTOR CoDKnight : HellKnight 5010
{
	RenderStyle None
	+NOTARGET
	Obituary "%o never saw %h fate."
	HitObituary ""
}

//===========================================================================
//
// CodBruiser
//
//===========================================================================
ACTOR CoDBruiser : BaronOfHell 5011
{
	States
	{
	Spawn:
		BOSS O 10 A_Look
		loop
	See:
		Goto Super::Raise
	Raise:
		Stop
	}
}

//===========================================================================
//
// CodCyber
//
//===========================================================================
ACTOR CoDCyber : Cyberdemon 5014
{
	States
	{
	Missile:
		CYBR E 6 A_FaceTarget
		CYBR F 12 A_CyberAttack
		CYBR E 12 A_FaceTarget
		CYBR F 12 A_CyberAttack
		CYBR E 12 A_FaceTarget
		CYBR F 0 A_JumpIfHealthLower(500, 2)
		CYBR F 12 A_CyberAttack
		Goto See
		CYBR F 12 A_CustomMissile("BFGBall", 32, 0)
		Goto See
	}
}

//===========================================================================
//
// CodVile
//
//===========================================================================
ACTOR CoDVile : Archvile 5017
{
	MissileType CoDVileSpawner
	RenderStyle Translucent 
	Alpha 0.5
	States
	{
	Missile:
		VILE G 1 BRIGHT A_VileStart
		VILE G 9 BRIGHT A_FaceTarget
		VILE H 8 BRIGHT A_VileTarget
		VILE IJKLMN 8 BRIGHT A_FaceTarget
		VILE O 8 BRIGHT A_MissileAttack
		VILE P 20 BRIGHT
		Goto See
	}
}
  
ACTOR CoDVileSpawner
{
	Projectile
	+SEEKERMISSILE
	States
	{
	Spawn:
		TNT1 A 1 A_Fire
		Loop
	Death:
		TNT1 A 0 A_Jump(253,2)
		TNT1 A 1 A_SpawnItem("ArchvileGhost", 1, 0)
		Stop
		
		TNT1 A 0 A_Jump(245,2)
		TNT1 A 1 A_SpawnItem("RevenantGhost", 1, 0)
		Stop
		
		TNT1 A 0 A_Jump(236,2)
		TNT1 A 1 A_SpawnItem("ArachnotronGhost", 1, 0)
		Stop
		
		TNT1 A 0 A_Jump(229,2)
		TNT1 A 1 A_SpawnItem("FatsoGhost", 1, 0)
		Stop
		
		TNT1 A 0 A_Jump(217,2)
		TNT1 A 1 A_SpawnItem("DoomImpGhost", 1, 0)
		Stop
		
		TNT1 A 0 A_Jump(250,2)
		TNT1 A 1 A_SpawnItem("PainElementalGhost", 1, 0)
		Stop
		
		TNT1 A 0 A_Jump(250,2)
		TNT1 A 1 A_SpawnItem("BaronGhost", 1, 0)
		Stop
		
		TNT1 A 0 A_Jump(243,2)
		TNT1 A 1 A_SpawnItem("HellKnightGhost", 1, 0)
		Stop
		
		TNT1 A 0 A_Jump(243,2)
		TNT1 A 1 A_SpawnItem("CacodemonGhost", 1, 0)
		Stop
		
		TNT1 A 0 A_Jump(247,2)
		TNT1 A 1 A_SpawnItem("SpectreGhost", 1, 0)
		Stop
		
		TNT1 A 1 A_SpawnItem("DemonGhost", 1, 0)
		Stop
		
	}
}

//===========================================================================
//
// Ghost monsters
//
//===========================================================================
ACTOR DoomImpGhost : DoomImp
{
	+NOCLIP
	RenderStyle Translucent
	Alpha 0.5
}

ACTOR DemonGhost : Demon
{
	+NOCLIP
	RenderStyle Translucent
	Alpha 0.5
}

ACTOR SpectreGhost : Spectre
{
	+NOCLIP
}

ACTOR CacodemonGhost : Cacodemon
{
	+NOCLIP
	RenderStyle Translucent
	Alpha 0.5
}

ACTOR HellKnightGhost : HellKnight
{
	+NOCLIP
	RenderStyle Translucent
	Alpha 0.5
}

ACTOR BaronGhost : BaronOfHell
{
	+NOCLIP
	RenderStyle Translucent
	Alpha 0.5
}

ACTOR RevenantGhost : Revenant
{
	+NOCLIP
	RenderStyle Translucent
	Alpha 0.5
}

ACTOR ArachnotronGhost : Arachnotron
{
	+NOCLIP
	RenderStyle Translucent
	Alpha 0.5
}

ACTOR PainElementalGhost : PainElemental
{
	+NOCLIP
	RenderStyle Translucent
	Alpha 0.5
}

ACTOR ArchvileGhost : Archvile
{
	+NOCLIP
	RenderStyle Translucent
	Alpha 0.5
}

ACTOR FatsoGhost : Fatso
{
	+NOCLIP
	RenderStyle Translucent
	Alpha 0.5
}

//===========================================================================
//
// Hardass Zombie
//
//===========================================================================
ACTOR HardAss : ZombieMan 5015
{
	Health 80
	Obituary "%o was surprised by the zombie's armament."
	DropItem RocketAmmo
	+DONTHURTSPECIES
	States
	{
	Missile:
		POSS E 10 A_FaceTarget
		POSS F 8 A_CyberAttack
		POSS E 8
		goto See
	}
}

//===========================================================================
//
// CodSpider
//
//===========================================================================
ACTOR CoDSpider : SpiderMastermind 5009
{
	States
	{
	Death:
		SPID J 20 A_Scream
		SPID K 0 A_Fall
		SPID K 10 A_SpawnItem("Arachnotron", 1, 0)
		SPID LMNOPQR 10
		SPID S 30
		SPID S -1
		Stop
	}
}

//===========================================================================
//
// Trigger monsters for boss battle
//
//===========================================================================

ACTOR TriggerZombie : ZombieMan 5029
{
	States
	{
	Raise:
		Stop
	Death:
		POSS H 5
		POSS I 5 A_Scream
		POSS J 5 A_Fall
		POSS K 5
		POSS L -1 Floor_LowerToLowest(666,8)
		Stop
	XDeath:
		POSS M 5
		POSS N 5 A_XScream
		POSS O 5 A_Fall
		POSS PQRST 5
		POSS U -1 Floor_LowerToLowest(666,8)
		Stop
	}
}	


ACTOR TriggerShotgunGuy : ShotgunGuy 5030
{
	States
	{
	Raise:
		Stop
	Death:
		SPOS H 5
		SPOS I 5 A_Scream
		SPOS J 5 A_Fall
		SPOS K 5
		SPOS L -1 Floor_LowerToLowest(667,8)
		Stop
	XDeath:
		SPOS M 5
		SPOS N 5 A_XScream
		SPOS O 5 A_Fall
		SPOS PQRST 5
		SPOS U -1 Floor_LowerToLowest(667,8)
		Stop
	}
}

ACTOR TriggerSpectre : Spectre 5031
{
	States
	{
	Raise:
		Stop
	Death:
		SARG I 8
		SARG J 8 A_Scream
		SARG K 4
		SARG L 4 A_Fall
		SARG M 4
		SARG N -1 Floor_LowerToLowest(668,8)
		Stop
	}
}


ACTOR TriggerImp : DoomImp 5032
{
	States
	{
	Raise:
		Stop
	Death:
		TROO I 8
		TROO J 8 A_Scream
		TROO K 6
		TROO L 6 A_Fall
		TROO M -1 Floor_LowerToLowest(135,8)
		Stop
	XDeath:
		TROO N 5
		TROO O 5 A_XScream
		TROO P 5
		TROO Q 5 A_Fall
		TROO RST 5
		TROO U -1 Floor_LowerToLowest(135,8)
		Stop
	}
}  

ACTOR TriggerDemon : Demon 5033
{
	States
	{
	Raise:
		Stop
	Death:
		SARG I 8
		SARG J 8 A_Scream
		SARG K 4
		SARG L 4 A_Fall
		SARG M 4
		SARG N -1 Floor_LowerToLowest(132,8)
		Stop
	}
}


ACTOR TriggerSkull : LostSoul 5034
{
	States
	{
	Raise:
		Stop
	Death:
		SKUL F 6 BRIGHT
		SKUL G 6 BRIGHT A_Scream
		SKUL H 6 BRIGHT
		SKUL I 6 BRIGHT A_Fall
		SKUL J 6
		SKUL K 6 Floor_LowerToLowest(671,8)
		Stop
	}	
}


ACTOR TriggerCacodemon : Cacodemon 5035
{
	States
	{
	Raise:
		Stop
	Death:
		HEAD G 8
		HEAD H 8 A_Scream
		HEAD I 8
		HEAD J 8
		HEAD K 8 A_Fall
		HEAD L 0 Floor_LowerToLowest(131,8)
		HEAD L -1 A_SetFloorClip
		Stop
	}
}  


ACTOR TriggerBaron : BaronOfHell 5036
{
	States
	{
	Raise:
		Stop
	Death:
		BOSS I  8
		BOSS J  8 A_Scream
		BOSS K  8
		BOSS L  8 A_Fall
		BOSS MN 8
		BOSS O -1 Floor_LowerToLowest(129,8)
		Stop
	}
}  


ACTOR TriggerCyber : Cyberdemon 5037
{
	States
	{
	Raise:
		Stop
	Death:
		CYBR H 10
		CYBR I 10 A_Scream
		CYBR JKL 10
		CYBR M 10 A_Fall
		CYBR NO 10
		CYBR P 30
		CYBR P -1 Floor_LowerToLowest(128,8)
		Stop
	}
}


ACTOR TriggerChaingunGuy : ChaingunGuy 5039
{
	States
	{
	Raise:
		Stop
	Death:
		CPOS H 5
		CPOS I 5 A_Scream
		CPOS J 5 A_Fall
		CPOS KLM 5
		CPOS N -1 Floor_LowerToLowest(676,8)
		Stop
	XDeath:
		CPOS O 5 
		CPOS P 5 A_XScream
		CPOS Q 5 A_Fall
		CPOS RS 5
		CPOS T -1 Floor_LowerToLowest(676,8)
		Stop
	}
}  


ACTOR TriggerHellknight : HellKnight 5040
{
	States
	{
	Raise:
		Stop
	Death:
		BOS2 I  8
		BOS2 J  8 A_Scream
		BOS2 K  8
		BOS2 L  8 A_Fall
		BOS2 MN 8
		BOS2 O -1 Floor_LowerToLowest(130,8)
		Stop
	}
}


ACTOR TriggerPainElemental : PainElemental 5042
{
	States
	{
	Raise:
		Stop
	Death:
		PAIN H 8
		PAIN I 8 A_Scream
		PAIN JK 8
		PAIN L 8 A_PainDie
		PAIN M 8 Floor_LowerToLowest(679,8)
		Stop
	}
}  


ACTOR TriggerRevenant : Revenant 5043
{
	States
	{
	Raise:
		Stop
	Death:
		SKEL LM 7
		SKEL N 7 A_Scream
		SKEL O 7 A_Fall
		SKEL P 7
		SKEL Q -1 Floor_LowerToLowest(134,8)
		Stop
	}
}  


ACTOR TriggerFatso : Fatso 5044
{
	States
	{
	Raise:
		Stop
	Death:
		FATT K 6
		FATT L 6 A_Scream
		FATT M 6 A_Fall
		FATT NOPQRS 6
		FATT T -1 Floor_LowerToLowest(133,8)
	     Stop
	}
}  


//===========================================================================
//
// Ambient Sounds
//
//===========================================================================

ACTOR AmbientBuzz 4988
{
	+NOSECTOR +NOBLOCKMAP
	States
	{
	Spawn:
		TNT1 A 15
		TNT1 A 10 A_PlaySoundEx("ambient/buzz", "body", 1)
		Wait
	}
}

ACTOR AmbientComp 4989
{
	+NOSECTOR +NOBLOCKMAP
	States
	{
	Spawn:
		TNT1 A 15
		TNT1 A 10 A_PlaySoundEx("ambient/comp", "body", 1)
		Wait
	}
}

ACTOR AmbientDrip 4990
{
	+NOSECTOR +NOBLOCKMAP
	States
	{
	Spawn:
		TNT1 A 15
		TNT1 A 10 A_PlaySoundEx("ambient/drip", "body", 1)
		Goto Spawn+1
	}
}

ACTOR AmbientFire 4991
{
	+NOSECTOR +NOBLOCKMAP
	States
	{
	Spawn:
		TNT1 A 15
		TNT1 A 10 A_PlaySoundEx("ambient/fire", "body", 1)
		Wait
	}
}

ACTOR AmbientLava 4992
{
	+NOSECTOR +NOBLOCKMAP
	States
	{
	Spawn:
		TNT1 A 15
		TNT1 A 10 A_PlaySoundEx("ambient/lava", "body", 1)
		Wait
	}
}

ACTOR AmbientRiver 4993
{
	+NOSECTOR +NOBLOCKMAP
	States
	{
	Spawn:
		TNT1 A 15
		TNT1 A 10 A_PlaySoundEx("ambient/river", "body", 1)
		Wait
	}
}

ACTOR AmbientWaterfall 4994
{
	+NOSECTOR +NOBLOCKMAP
	States
	{
	Spawn:
		TNT1 A 15
		TNT1 A 10 A_PlaySoundEx("ambient/waterfall", "body", 1)
		Wait
	}
}

ACTOR AmbientWind 4995
{
	+NOSECTOR +NOBLOCKMAP
	States
	{
	Spawn:
		TNT1 A 15
		TNT1 A 10 A_PlaySoundEx("ambient/wind", "body", 1)
		Wait
	}
}

ACTOR AmbientWind2 4996
{
	+NOSECTOR +NOBLOCKMAP
	States
	{
	Spawn:
		TNT1 A 15
		TNT1 A 10 A_PlaySoundEx("ambient/wind2", "body", 1)
		Wait
	}
}

ACTOR CODKeenDummy 18890
{
	Health 1
	Radius 16
	Height 72
	Mass 10000000
	PainChance 256
	+SOLID 
	+SPAWNCEILING 
	+NOGRAVITY 
	+SHOOTABLE 
	+COUNTKILL 
	+NOICEDEATH
	+ISMONSTER
	PainSound "biowar/null"
	DeathSound "biowar/null"
	States
	{
	Spawn:
		TNT1 A -1
		Loop
	Death:
		TNT1 A 1 A_KeenDie
		TNT1 A -1
		Stop
	Pain:
		TNT1 A 1
		Goto Spawn
	}
}