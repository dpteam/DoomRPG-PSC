ACTOR CleimosRobot 18690 // 84
{
	Health 50
	Radius 20
	Height 56
	Speed 8
	PainChance 170
	Monster
	+FLOORCLIP
	+NOBLOOD
	SeeSound "CleimosSound1"
	PainSound "CleimosSound3"
	DeathSound "CleimosSound4"
	ActiveSound "wolfss/active"
	AttackSound "CleimosSound2"
	Obituary "%o was shot dead"
	Dropitem "Clip"
	States
	{
	Spawn:
		CLEM AB 10 A_Look
		Loop
	See:
		CLEM AABBCCDD 3 A_Chase
		Loop
	Missile:
		CLEM E 10 A_FaceTarget
		CLEM F 10 A_FaceTarget
		CLEM G 4 BRIGHT A_CPosAttack
		CLEM F 6 A_FaceTarget
		CLEM G 4 BRIGHT A_CPosAttack
		CLEM F 1 A_CPosRefire
		Goto Missile+1
	Pain:
		CLEM H 3
		CLEM H 3 A_Pain
		Goto See
	Death:
		CLEM I 5
		CLEM J 5 A_Scream
		CLEM K 5 A_NoBlocking
		CLEM L 5
		CLEM M -1
		Stop
	XDeath:
		CLEM N 5 
		CLEM O 5 A_Scream
		CLEM P 5 A_NoBlocking
		CLEM QRSTU 5
		CLEM V -1
		Stop
	Raise:
		CLEM M 5
		CLEM LKJI 5
		Goto See 
	}
}

ACTOR CleimosForcefield 18691 // 63
{
	Radius 16
	Height 68
	+SOLID
	+NOGRAVITY
	+SPAWNCEILING
	States
	{
	Spawn:
		CLEM W 10
		CLEM X 15
		CLEM Y 8
		CLEM X 6
		Loop
	}
}

ACTOR CleimosForcefield0 : CleimosForcefield 18692 // 49
{
	-SOLID
	Radius 20
}

ACTOR CleimosBrain 18693 // 88
{
	Health 250
	Mass 10000000
	PainChance 255
	+SOLID +SHOOTABLE
	+NOICEDEATH
	+OLDRADIUSDMG
	+BOSS
	PainSound "equinox/warning"
	DeathSound "equinox/explode"
	States
	{
	BrainExplode:
		MISL BC 10 Bright
		MISL D 10 A_BrainExplode
		Stop
	Spawn:
		CLEM "[" -1
		Stop
	Pain:
		CLEM "]" 36 A_PlaySound("equinox/warning",CHAN_VOICE,1.0,0,ATTN_NONE)
		Goto Spawn
	Death:
		CLEM "[" 0 A_BrainScream
		CLEM "[" 100 A_Scream
		CLEM "[[" 10
		CLEM "[" -1 A_BrainDie
		Stop
	}
}
