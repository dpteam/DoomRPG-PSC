ACTOR SSPredatorSpectre 18863 // 84
{
	Health 80
	Radius 20
	Height 56
	Speed 8
	PainChance 150
	RenderStyle OptFuzzy
	Alpha 0.5
	Monster
	+FLOAT
	+NOGRAVITY
	+SHADOW
	SeeSound "twzone2/snd1"
	PainSound "grunt/pain"
	DeathSound "grunt/death"
	ActiveSound "twzone2/snd1"
	AttackSound "wolfss/attack"
	Obituary "&o was killed by a SS Predator Spectre"
	Dropitem "Clip"
	States
	{
	Spawn:
		SSWV AB 10 A_Look
		Loop
	See:
		SSWV AABBCCDD 3 A_Chase
		Loop
	Missile:
		SSWV E 10 A_FaceTarget
		SSWV F 10 A_FaceTarget
		SSWV G 4 BRIGHT A_CPosAttack
		SSWV F 6 A_FaceTarget
		SSWV G 4 BRIGHT A_CPosAttack
		SSWV F 1 A_CPosRefire
		Goto Missile+1
	Pain:
		SSWV H 3
		SSWV H 3 A_Pain
		Goto See
	Death:
		SSWV I 5
		SSWV J 5 A_Scream
		SSWV K 5 A_NoBlocking
		SSWV L 5
		SSWV M -1
		Stop
	XDeath:
		SSWV N 5 
		SSWV O 5 A_XScream
		SSWV P 5 A_NoBlocking
		SSWV QRSTU 5
		SSWV V -1
		Stop
	Raise:
		SSWV M 5
		SSWV LKJI 5
		Goto See 
	}
}

ACTOR JohnnysMarine 18864 // 7
{
	Health 1500
	Radius 20
	Height 56
	Mass 100
	Speed 15
	PainChance 108
	Monster
	MinMissileChance 160
	+BOSS
	+MISSILEMORE
	+FLOORCLIP
	+NORADIUSDMG
	+DONTMORPH
	+BOSSDEATH
	SeeSound "twzone2/snd3"
	AttackSound "grunt/attack"
	PainSound "twzone2/snd8"
	DeathSound "twzone2/snd4"
	ActiveSound "spider/active"
	Obituary "%o was killed by Johnny's Baby Cyberdemon Marine"
	States
	{
	Spawn:
		TWZ2 AB 10 A_Look
		Loop
	See:
		TWZ2 AABBCCDD 2 A_Chase
		Loop
	Missile:
		TWZ2 A 12 BRIGHT A_FaceTarget  
		TWZ2 G 2 BRIGHT A_CPosAttack
		TWZ2 H 2 BRIGHT A_CPosAttack
		TWZ2 H 0 BRIGHT A_SpidRefire
		Goto Missile+1
	Pain:
		TWZ2 I 3
		TWZ2 I 3 A_Pain
		Goto See
	Death:
		TWZ2 J 18 A_Scream
		TWZ2 K 6 A_NoBlocking
		TWZ2 LMNOPQR 6
		TWZ2 S 30
		TWZ2 S -1 A_BossDeath    
		Stop
	}
}

ACTOR JohnnyBoy 18865
{
	Health 3000
	Mass 10000000
	PainChance 255
	+SOLID +SHOOTABLE
	+NOICEDEATH
	+OLDRADIUSDMG
	+BOSS
	PainSound "twzone2/snd6"
	DeathSound "twzone2/snd7"
	States
	{
	BrainExplode:
		MISL BC 10 Bright
		MISL D 10 A_BrainExplode
		Stop
	Spawn:
		TWZ2 T -1
		Stop
	Pain:
		TWZ2 U 36 A_PlaySound("twzone2/snd6",CHAN_VOICE,1.0,0,ATTN_NONE)
		Goto Spawn
	Death:
		TWZ2 T 0 A_BrainScream
		TWZ2 T 125 A_Scream
		TWZ2 TT 10
		TWZ2 T -1 A_BrainDie
		Stop
	}
}


ACTOR JohnnyBoyEye 18866 
{
	Height 32
	+NOBLOCKMAP
	+NOSECTOR
	+LOOKALLAROUND
	States
	{
	Spawn:
		TNT1 A 10 A_Look
		Loop
	See:
		TNT1 A 190 A_PlaySound("twzone2/snd5",CHAN_VOICE,1.0,0,ATTN_NONE)
		TNT1 A 150 A_BrainSpit
		Wait
	}
}
