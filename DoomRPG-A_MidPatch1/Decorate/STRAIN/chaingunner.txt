actor Chaingunner 20010 {
	obituary "%o was perforated by a chaingunner."
	health 70
	radius 20
	height 56
	mass 100
	speed 8
	painchance 170
	seesound "chainguy/sight"
	attacksound "chaingunner/attack"
	painsound "chainguy/pain"
	deathsound "chainguy/death"
	activesound "chainguy/active"
	dropitem "Chaingun" 256
	MONSTER
	+FLOORCLIP
	states {
		Spawn:
			GPOS AB 10 A_Look
			loop
		See:
			GPOS AABBCCDD 3 A_Chase
			loop
		Missile:
			GPOS E 10 A_FaceTarget
			GPOS FE 4 bright A_CPosAttack
			GPOS F 1 A_CPosRefire
			goto Missile+1
		Pain:
			GPOS G 3
			GPOS G 3 A_Pain
			goto See
		Death:
			GPOS H 5
			GPOS I 5 A_Scream
			GPOS J 5 A_Fall
			GPOS KLM 5
			GPOS N -1
			stop
		XDeath:
			GPOS O 5
			GPOS P 5 A_XScream
			GPOS Q 5 A_Fall
			GPOS RS 5
			GPOS T -1
			stop
		Raise:
			GPOS NMLKJIH 5
			goto See
	}
}