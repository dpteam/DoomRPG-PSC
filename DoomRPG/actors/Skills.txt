// Powerups
actor DRPGSkillInvulnerability1 : PowerupGiver
{
    -INVBAR
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ADDITIVETIME
    
    Powerup.Type "Invulnerable"
    Powerup.Color "GoldMap"
    Powerup.Duration -30
}

actor DRPGSkillInvulnerability2 : DRPGSkillInvulnerability1
{
    Powerup.Mode Reflective
}

actor DRPGSkillInvisibility1 : PowerupGiver
{
    -INVBAR
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ADDITIVETIME
    
    Powerup.Type "Shadow"
    Powerup.Strength 100
    Powerup.Duration -30
}

actor DRPGSkillInvisibility2 : DRPGSkillInvisibility1
{
    Powerup.Type "Ghost"
}

actor DRPGSkillIronFeet : PowerupGiver
{
    -INVBAR
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ADDITIVETIME
    
    Powerup.Type "IronFeet"
    Powerup.Duration -60
}

actor DRPGSkillLightAmp : PowerupGiver
{
    -INVBAR
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ADDITIVETIME
    
    Powerup.Type "LightAmp"
}

actor DRPGTimeFreezerQuick : PowerupGiver
{
    -INVBAR
    +INVENTORY.AUTOACTIVATE

    Powerup.Type "TimeFreezer"
    Powerup.Color Black
    Powerup.Duration 3
}

// Used for SBARINFO Aura HUD checking, horrible hacks
actor DRPGAura : Inventory
{
    Inventory.Amount 1
    Inventory.MaxAmount 1
}

// Aura Skills
actor DRPGGreenAuraIronFeet : PowerIronFeet
{
    Powerup.Duration 5
    Powerup.Color 0, 0, 0, 0
}

actor DRPGPinkAuraDrain : PowerDrain
{
    Powerup.Duration 5
    Powerup.Strength 0.06
}

actor DRPGDarkBlueAuraInfiniteAmmo2 : PowerupGiver 
{
    +Inventory.AutoActivate
    Powerup.Type PowerInfiniteAmmo
    Powerup.Duration 5
}

// Bullet-Time Skill
actor DRPGSkillBulletTime : PowerupGiver
{
    -INVBAR
    
    +INVENTORY.AUTOACTIVATE
    -INVENTORY.ADDITIVETIME
    
    Powerup.Type TimeFreezer
    Powerup.Duration 60
}

// Repulsion Skill
actor DRPGSkillBlast1 : CustomInventory
{
    +INVENTORY.AUTOACTIVATE
    
    States
    {
    Spawn:
        TNT1 A 0
    Use:
        TNT1 A 0 A_Blast(0, 255, 255, 20, "BlastEffect", "skills/blast")
        Stop
    }
}

actor DRPGSkillBlast2 : DRPGSkillBlast1
{
    States
    {
    Use:
        TNT1 A 0 A_Blast(0, 255, 512, 50, "BlastEffect", "skills/blast")
        Stop
    }
}

actor DRPGSkillBlast3 : DRPGSkillBlast1
{
    States
    {
    Use:
        TNT1 A 0 A_Blast(0, 255, 1024, 100, "BlastEffect", "skills/blast")
        Stop
    }
}

actor DRPGSkillBlast4 : DRPGSkillBlast1
{
    States
    {
    Use:
        TNT1 A 0 A_Blast(0, 255, 2048, 200, "BlastEffect", "skills/blast")
        Stop
    }
}

// Disruption
actor DRPGMonsterDisrupted : Inventory { Inventory.MaxAmount 1050 +INVENTORY.UNDROPPABLE }

actor DRPGAreaDisruptionSmall : CustomInventory
{
    Inventory.MaxAmount 0
    +INVENTORY.AUTOACTIVATE
    
    States
    {
    Pickup:
        TNT1 A 0 A_RadiusGive("DRPGMonsterDisrupted", 512, RGF_MONSTERS, 1050)
        Stop
    }
}

actor DRPGAreaDisruptionLarge : CustomInventory
{
    Inventory.MaxAmount 0
    +INVENTORY.AUTOACTIVATE
    
    States
    {
    Pickup:
        TNT1 A 0 A_RadiusGive("DRPGMonsterDisrupted", 1024, RGF_MONSTERS, 1050)
        Stop
    }
}

// Force Field
actor DRPGForceWall
{
    Radius 32
    Height 64
    
    Health 100
    PainChance 256
    Mass 0x7FFFFFF
    
    Species "Player"
    
    Monster
    -COUNTKILL
    +SOLID
    +NOBLOOD
    +BRIGHT
    +REFLECTIVE
    +DEFLECT
    +THRUSPECIES
    +FRIENDLY
    
    RenderStyle Add
    Alpha 0.5
    
    Tag "Force Wall"
    
    var int user_step;
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0, MF_NOXP | MF_NOSTATS | MF_NOAURA | MF_NOPAYKILL | MF_NODROPS | MF_NOWEAKEN)
    Pulse:
        FFLD A 1
        TNT1 A 0 A_SetTranslucent(0.5 + (0.25 * Sin(user_step)), 1)
        TNT1 A 0 A_SetUserVar("user_step", user_step + 2.5714285)
        Loop
    Pain: // Temporary effect?
        TNT1 A 0 A_SpawnItemEx("DRPGBlingParticle", FRandom(-Radius, Radius), FRandom(-Radius, Radius), Height / 2, FRandom(-4.0, 4.0), FRandom(-4.0, 4.0), FRandom(-4.0, 4.0))
        Goto Pulse
    Death: // Temporary effect?
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("DRPGBlingParticle", FRandom(-Radius, Radius), FRandom(-Radius, Radius), Height * 2, FRandom(-4.0, 4.0), FRandom(-4.0, 4.0), FRandom(-4.0, 4.0))
        FFLD A 1 A_FadeOut(0.01, true)
        Loop
    }
}

actor DRPGTranslocateBall : TelOtherFX1
{
    Radius 6
    Height 1
    Speed 20
    
    SeeSound "weapons/bfgf"
    DeathSound "weapons/bfgx"
    
    Scale 0.5
    
    States
    {
    Spawn:
        BFS1 AB 4 Bright
        Loop
    Death:
        BFE1 ABCDEF 8 Bright
        Stop
    }
}

// Souls
actor DRPGSoulBase : CustomInventory
{
    Inventory.PickupMessage "Picked up a Soul."
    Inventory.PickupSound "skills/buff"
    
    Scale 0.5
    Gravity 0.25
    
    RenderStyle Add
    
    +FLOATBOB
}

actor DRPGSoulRed : DRPGSoulBase
{
    Inventory.PickupMessage "Picked up a \cgRed\c- Soul."
    
    States
    {
    Spawn:
        SOL1 ABCD 4 Bright
        Loop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteAlways("AddCompound", 0, 0, 10)
        TNT1 A 0 ACS_NamedExecuteAlways("SoulEffect", 0, 0)
        TNT1 A 0 A_GiveInventory("DRPGSoulRedToken", Random(1,2))
        Stop
    }
}

actor DRPGSoulGreen : DRPGSoulBase
{
    Inventory.PickupMessage "Picked up a \cdGreen\c- Soul."
    
    States
    {
    Spawn:
        SOL2 ABCD 4 Bright
        Loop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteAlways("AddCompound", 0, 1, 10)
        TNT1 A 0 ACS_NamedExecuteAlways("SoulEffect", 0, 1)
        TNT1 A 0 A_GiveInventory("DRPGSoulGreenToken", Random(1,2))
        Stop
    }
}

actor DRPGSoulWhite : DRPGSoulBase
{
    Inventory.PickupMessage "Picked up a \cjWhite\c- Soul."
    
    States
    {
    Spawn:
        SOL3 ABCD 4 Bright
        Loop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteAlways("AddCompound", 0, 9, 10)
        TNT1 A 0 ACS_NamedExecuteAlways("SoulEffect", 0, 2)
        TNT1 A 0 A_GiveInventory("DRPGSoulWhiteToken", 1)
        Stop
    }
}

actor DRPGSoulPink : DRPGSoulBase
{
    Inventory.PickupMessage "Picked up a \caPink\c- Soul."
    
    States
    {
    Spawn:
        SOL4 ABCD 4 Bright
        Loop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteAlways("AddCompound", 0, 2, 10)
        TNT1 A 0 ACS_NamedExecuteAlways("SoulEffect", 0, 3)
        TNT1 A 0 A_GiveInventory("DRPGSoulPinkToken", Random(1,2))
        Stop
    }
}

actor DRPGSoulBlue : DRPGSoulBase
{
    Inventory.PickupMessage "Picked up a \cnBlue\c- Soul."
    
    States
    {
    Spawn:
        SOL5 ABCD 4 Bright
        Loop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteAlways("AddCompound", 0, 3, 10)
        TNT1 A 0 ACS_NamedExecuteAlways("SoulEffect", 0, 4)
        TNT1 A 0 A_GiveInventory("DRPGSoulBlueToken", Random(1,2))
        Stop
    }
}

actor DRPGSoulPurple : DRPGSoulBase
{
    Inventory.PickupMessage "Picked up a \ctPurple\c- Soul."
    
    States
    {
    Spawn:
        SOL6 ABCD 4 Bright
        Loop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteAlways("AddCompound", 0, 4, 10)
        TNT1 A 0 ACS_NamedExecuteAlways("SoulEffect", 0, 5)
        TNT1 A 0 A_GiveInventory("DRPGSoulPurpleToken", Random(1,2))
        Stop
    }
}

actor DRPGSoulOrange : DRPGSoulBase
{
    Inventory.PickupMessage "Picked up a \ciOrange\c- Soul."
    
    States
    {
    Spawn:
        SOL7 ABCD 4 Bright
        Loop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteAlways("AddCompound", 0, 5, 10)
        TNT1 A 0 ACS_NamedExecuteAlways("SoulEffect", 0, 6)
        TNT1 A 0 A_GiveInventory("DRPGSoulOrangeToken", Random(1,2))
        Stop
    }
}

actor DRPGSoulDarkBlue : DRPGSoulBase
{
    Inventory.PickupMessage "Picked up a \chDark Blue\c- Soul."
    
    States
    {
    Spawn:
        SOL8 ABCD 4 Bright
        Loop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteAlways("AddCompound", 0, 6, 10)
        TNT1 A 0 ACS_NamedExecuteAlways("SoulEffect", 0, 7)
        TNT1 A 0 A_GiveInventory("DRPGSoulDarkBlueToken", Random(1,2))
        Stop
    }
}

actor DRPGSoulYellow : DRPGSoulBase
{
    Inventory.PickupMessage "Picked up a \ckYellow\c- Soul."
    
    States
    {
    Spawn:
        SOL9 ABCD 4 Bright
        Loop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteAlways("AddCompound", 0, 7, 10)
        TNT1 A 0 ACS_NamedExecuteAlways("SoulEffect", 0, 8)
        TNT1 A 0 A_GiveInventory("DRPGSoulYellowToken", Random(1,2))
        Stop
    }
}

// Souls Tokens
actor DRPGSoulRedToken : Inventory       { Inventory.MaxAmount 1000 }
actor DRPGSoulGreenToken : Inventory     { Inventory.MaxAmount 1000 }
actor DRPGSoulWhiteToken : Inventory     { Inventory.MaxAmount 1000 }
actor DRPGSoulPinkToken : Inventory      { Inventory.MaxAmount 1000 }
actor DRPGSoulBlueToken : Inventory      { Inventory.MaxAmount 1000 }
actor DRPGSoulPurpleToken : Inventory    { Inventory.MaxAmount 1000 }
actor DRPGSoulOrangeToken : Inventory    { Inventory.MaxAmount 1000 }
actor DRPGSoulDarkBlueToken : Inventory  { Inventory.MaxAmount 1000 }
actor DRPGSoulYellowToken : Inventory    { Inventory.MaxAmount 1000 }

// Mean Things
actor DRPGEPDrain : CustomInventory
{
    +INVENTORY.AUTOACTIVATE
    +NOINTERACTION
    
    States
    {
    Spawn:
        TNT1 A 0
    Use:
        TNT1 A 0 ACS_NamedExecuteAlways("AddEP", 0, -1)
        Stop
    }
}

actor DRPGAuraRemover : CustomInventory
{
    +INVENTORY.AUTOACTIVATE
    +NOINTERACTION
    
    States
    {
    Spawn:
        TNT1 A 0
    Use:
        TNT1 A 0 ACS_NamedExecuteAlways("RemoveAura", 0)
        Stop
    }
}

// Attack Skills

// Plasma Beam
// -------------------------------------------------------

actor DRPGSkillPlasmaBeam1 : CustomInventory
{
    Decal "BaronScorch"
    
    States
    {
    Pickup:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_RailAttack(4, 0, false, "None", "Orange", RGF_SILENT | RGF_NOPIERCING, 0, "DRPGSkillPlasmaBeam1FirePuff", 0, 0, 1024, 0, 32.0, 0, "DRPGSkillPlasmaBeam1FlameSpark", -8)
        Stop
    }
}

actor DRPGSkillPlasmaBeam1FirePuff : BulletPuff
{
    DamageType "Fire"
    Decal "BaronScorch"
    
    +ALWAYSPUFF
    +BLOODLESSIMPACT
    +FORCEDECAL
    -RANDOMIZE
    -ALLOWPARTICLES
    
    States
    {
    Spawn:
        TNT1 A 2
        Stop
    }
}

actor DRPGSkillPlasmaBeam1FlameSpark
{
    RenderStyle Add
    Alpha 0.33
    Scale 0.5
    Radius 8
    Height 8
    
    +NOINTERACTION
    +NOTIMEFREEZE
    -RANDOMIZE
    
    States
    {
    Spawn:
        PBM2 A 0 NoDelay A_Warp(AAPTR_DEFAULT, FRandom(-3,3),FRandom(-3,3),FRandom(-3,3), 0, WARPF_INTERPOLATE)
        PBM2 A 0 A_Jump(256, "Sprite1", "Sprite2", "Sprite3")
    Sprite1:
        PBM2 A 2 Bright
        Goto End
    Sprite2:
        PBM2 B 2 Bright
        Goto End
    Sprite3:
        PBM2 C 2 Bright
        Goto End
    End:
        PBM2 C 0 A_SpawnItemEx("DRPGSkillPlasmaBeam1FlameSpark2", 0, FRandom(-16, 16), FRandom(-16, 16), FRandom(1, 3), FRandom(-0.25, 0.25), FRandom(-0.25, 0.25), 0, 0, 128)
        PBM2 C 0 A_SpawnItemEx("DRPGSkillPlasmaBeam1FlameEmber", 0, FRandom(-16, 16), FRandom(-16, 16), FRandom(-5, 5), FRandom(-5, 5), FRandom(-0, 5), 0, 0, 255)
        Stop
    }
}

actor DRPGSkillPlasmaBeam1FlameSpark2
{
    RenderStyle Add
    Alpha 0.25
    Scale 0.05
    Radius 8
    Height 8
    
    +NOINTERACTION
    +NOTIMEFREEZE
    -RANDOMIZE
    
    States
    {
    Spawn:
        PTCL K 2 Bright
    ShrinkAway:
        PTCL K 1 Bright A_SetScale(ScaleX - 0.0014)
        PTCL K 0 Bright A_JumpIf(ScaleX <= 0, "Death")
        Loop
    Death:
        PTCL K 0 Bright
        Stop
    }
}

actor DRPGSkillPlasmaBeam1FlameEmber
{
    RenderStyle Add
    Alpha 1.0
    Scale 0.025
    Radius 8
    Height 8
    Gravity 0.33
    
    Projectile
    +NOTIMEFREEZE
    -NOGRAVITY
    -RANDOMIZE
    +MOVEWITHSECTOR
    
    States
    {
    Spawn:
        PTCL L -1
        Stop
    Death:
    ShrinkAway:
        PTCL LLLLLLLLLLLLLLLLLLLL 1 Bright A_SetScale(ScaleX - 0.001)
    Disappear:
        PTCL L 0 Bright
        Stop
    }
}

actor DRPGSkillPlasmaBeam2 : CustomInventory
{
    Decal "BaronScorch"
    
    States
    {
    Pickup:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_RailAttack(6, 0, false, "None", "Orange", RGF_SILENT,  0, "DRPGSkillPlasmaBeam1FirePuff",     0, 0, 2048, 0, 32.0, 0, "DRPGSkillPlasmaBeam1FlameSpark",    -8)
        TNT1 A 0 A_RailAttack(6, 0, false, "None", "White",  RGF_SILENT, 32, "DRPGSkillPlasmaBeam2ElectricPuff", 0, 0, 2048, 0,  8.0, 0, "DRPGSkillPlasmaBeam2ElectricSpark", -8)
        Stop
    }
}

actor DRPGSkillPlasmaBeam2ElectricPuff : DRPGSkillPlasmaBeam1FirePuff
{
    DamageType "Electric"
}

actor DRPGSkillPlasmaBeam2ElectricSpark : DRPGShieldLightningParticle
{
    Radius 4
    Height 4
    Alpha 1.0
    Scale 0.05
    
    +NOTIMEFREEZE
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_Jump(96, 1)
        Goto Death
    DecayLoop:
        SLGT A 5
        SLGT AAAAAAAAAAAAAAA 1 Bright A_SetScale(ScaleX - 0.0025)
    Death:
        TNT1 A 0
        Stop
    }
}

actor DRPGSkillPlasmaBeam3 : CustomInventory
{
    Decal "BaronScorch"
    
    States
    {
    Pickup:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_RailAttack( 0, 0, false, "None", "SkyBlue",    RGF_SILENT, 32, "DRPGSkillPlasmaBeam3DarknessPuff", 0, 0, 4096, 0,  8.0, 0, "DRPGSkillPlasmaBeam3ElectricSpark",     -12)
        TNT1 A 0 A_RailAttack( 0, 0, false, "None", "Maroon",     RGF_SILENT, 32, "DRPGSkillPlasmaBeam3DarknessPuff", 0, 0, 4096, 0,  8.0, 0, "DRPGSkillPlasmaBeam3DarkElectricSpark", -12)
        TNT1 A 0 A_RailAttack(20, 0, false, "None", "DarkViolet", RGF_SILENT,  0, "DRPGSkillPlasmaBeam3DarknessPuff", 0, 0, 4096, 0, 32.0, 0, "DRPGSkillPlasmaBeam3DarkFlameSpark",    -12)
        Stop
    }
}

actor DRPGSkillPlasmaBeam3DarknessPuff : DRPGSkillPlasmaBeam1FirePuff
{
    DamageType "Shadow"
    +PIERCEARMOR
}

actor DRPGSkillPlasmaBeam3DarkFlameSpark
{
    RenderStyle Subtract
    Alpha 0.25
    Scale 0.075
    Radius 8
    Height 8
    
    +NOINTERACTION
    +NOTIMEFREEZE
    -RANDOMIZE
    
    States
    {
    Spawn:
        //PTCL N 0 Bright NoDelay A_Warp(AAPTR_DEFAULT, FRandom(-4,4),FRandom(-4,4),FRandom(-4,4), 0, WARPF_INTERPOLATE)
    ShrinkAway:
        PTCL NNNNNNNNNNNNNNNNNNNN 1 Bright A_SetScale(ScaleX - 0.003)
    Disappear:
        PTCL N 0 Bright
        Stop
    }
}

actor DRPGSkillPlasmaBeam3ElectricSpark : DRPGShieldLightningParticle
{
    RenderStyle Add
    Radius 4
    Height 4
    Alpha 1.0
    Scale 0.025
    
    +NOTIMEFREEZE
    
    States
    {
    Spawn:
        PTCL M 0 NoDelay A_Jump(48, 1)
        Goto Death
        PTCL M 5
        PTCL MMMMMMMMMMMMMMM 1 Bright A_SetScale(ScaleX - 0.00125)
    Death:
        PTCL M 0
        Stop
    }
}

actor DRPGSkillPlasmaBeam3DarkElectricSpark : DRPGSkillPlasmaBeam3ElectricSpark
{
    RenderStyle Subtract
}